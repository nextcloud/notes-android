/*
 * Nextcloud Notes - Android Client
 *
 * SPDX-FileCopyrightText: 2015-2024 Nextcloud GmbH and Nextcloud contributors
 * SPDX-FileCopyrightText: 2015-2024 Stefan Niedermann <info@niedermann.it>
 * SPDX-FileCopyrightText: 2020 Hui-Ouyang16 <11710106@mail.sustech.edu.cn>
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    namespace = 'it.niedermann.owncloud.notes'

    defaultConfig {
        applicationId "it.niedermann.owncloud.notes"
        minSdk 24
        compileSdk = 36
        targetSdk 36
        versionCode 40070000
        versionName "4.7.0 Alpha 1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
    }

    testCoverage.jacocoVersion = "0.8.13"

    flavorDimensions = ["version"]

    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
        }
        qa {
            applicationIdSuffix ".qa"
            dimension "version"
            versionCode 11
            versionName "1"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }


    androidResources {
        generateLocaleConfig = true
    }

    packagingOptions {
        resources {
            excludes += "META-INF/LICENSE*"
            excludes += "META-INF/versions/9/OSGI-INF/MANIFEST*"
            pickFirst "MANIFEST.MF" // workaround for duplicated manifest on some dependencies
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
}

ext {
    glideVersion = '5.0.5'
    roomVersion = "2.8.4"
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation ('commons-httpclient:commons-httpclient:3.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }

    implementation("com.github.nextcloud:android-library:2.23.0") {
        exclude group: 'org.ogce', module: 'xpp3'
    }

    implementation("com.github.nextcloud.android-common:ui:$nextcloudAndroidCommonLib")
    implementation("com.github.nextcloud.android-common:core:$nextcloudAndroidCommonLib")

    // Nextcloud SSO
    implementation("com.github.nextcloud:Android-SingleSignOn:$singleSignOnVersion") {
        version {
            strictly(singleSignOnVersion)
        }
    }
    implementation "com.github.stefan-niedermann.android-commons:shared-preferences:$androidCommonsVersion"
	implementation "com.github.stefan-niedermann.android-commons:reactive-livedata:$androidCommonsVersion"
	implementation "com.github.stefan-niedermann.android-commons:util:$androidCommonsVersion"
    implementation "com.github.stefan-niedermann.nextcloud-commons:sso-glide:$commonsVersion"
    implementation "com.github.stefan-niedermann.nextcloud-commons:exception:$commonsVersion"
    implementation("com.github.stefan-niedermann.nextcloud-commons:markdown:$commonsVersion") {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Android X
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.core:core-splashscreen:1.2.0'
    implementation 'androidx.fragment:fragment:1.8.9'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'androidx.work:work-runtime:2.11.1'
    implementation 'com.google.android.material:material:1.13.0'
    
    // Vanitech
    implementation 'com.vanniktech:emoji-google:0.23.0'

    // Database
    implementation "androidx.room:room-runtime:${roomVersion}"
    annotationProcessor "androidx.room:room-compiler:${roomVersion}"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.13.2'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Testing
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.robolectric:robolectric:4.16.1'
}

configurations.configureEach {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "com.github.nextcloud.android-common") {
            details.useVersion(nextcloudAndroidCommonLib)
        }
    }
}

// Run the compiler as a separate process
tasks.withType(JavaCompile).configureEach {
    options.fork = true

    // Enable Incremental Compilation
    options.incremental = true
}

tasks.withType(Test).configureEach {
    // Run tests in parallel
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
