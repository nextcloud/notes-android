apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        applicationId "it.niedermann.owncloud.notes"
        minSdkVersion 22
        targetSdkVersion 31
        versionCode 3004016
        versionName "3.4.16"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }

    flavorDimensions "version"

    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
        }
        pfungstadt {
            dimension "version"
            applicationIdSuffix ".pfungstadt"
        }
        mdm {
            dimension "version"
            applicationIdSuffix ".mdm"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Nextcloud SSO
    implementation 'com.github.nextcloud:Android-SingleSignOn:0.6.0'
    implementation 'com.github.stefan-niedermann:android-commons:0.2.5'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:sso-glide:1.6.2'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:exception:1.6.2'
    implementation('com.github.stefan-niedermann.nextcloud-commons:markdown:1.6.2') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // Android X
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.fragment:fragment:1.4.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'com.google.android.material:material:1.4.0'

    // Database
    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.9'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    // Testing
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.0.0'
    testImplementation 'org.robolectric:robolectric:4.7.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
