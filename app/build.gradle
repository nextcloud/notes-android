apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 33

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    defaultConfig {
        applicationId "it.niedermann.owncloud.notes"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 40000000
        versionName "4.0.0 Alpha 1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    flavorDimensions "version"

    productFlavors {
        fdroid {
            dimension "version"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
        }
        play {
            dimension "version"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }
    lint {
        abortOnError false
        disable 'MissingTranslation'
    }
    namespace 'it.niedermann.owncloud.notes'
}

ext {
    glideVersion = '4.15.1'
    roomVersion = "2.5.1"
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    // Nextcloud SSO
    implementation 'com.github.nextcloud.android-common:ui:0.8.0'
    implementation 'com.github.nextcloud:Android-SingleSignOn:0.6.1'
    implementation 'com.github.stefan-niedermann:android-commons:0.2.9'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:sso-glide:1.6.4'
    implementation 'com.github.stefan-niedermann.nextcloud-commons:exception:1.6.4'
    implementation('com.github.stefan-niedermann.nextcloud-commons:markdown:1.6.4') {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }

    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Android X
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'androidx.fragment:fragment:1.5.6'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.work:work-runtime:2.8.1'
    implementation 'com.google.android.material:material:1.8.0'

    // Database
    implementation "androidx.room:room-runtime:${roomVersion}"
    annotationProcessor "androidx.room:room-compiler:${roomVersion}"

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.10.1'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // Testing
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.robolectric:robolectric:4.9.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
